# incl - copy f to STDOUT, replacing  include file by contents of file
   subroutine incl(f)
   integer f
   character line(MAXLINE), str(MAXLINE)
   integer equal, getlin, getqw, open, getwrd
   integer infile(NFILES), len, level, loc, i
   string incld "include"
   string bincld "INCLUDE"
 
   infile(1) = f
   for (level = 1; level > 0; level = level - 1) {
      while (getlin(line, infile(level)) ^= EOF) {
         loc = 1
         len = getwrd(line, loc, str)
         if ((equal(str, incld) == NO) &
             (equal(str, bincld) == NO))
            call putlin(line, STDOUT)
         else {
            level = level + 1
            if (level > NFILES)
               call error("includes nested too deeply.")
            len = getqw(line, loc, str)
            infile(level) = open(str, READ)
            if (infile(level) == ERR) {
               call putlin(str, ERROUT)
               call remark(": can't include.")
               level = level - 1
               }
            }
         }
      if (level > 1)
         call close(infile(level))
      }
   return
   end
